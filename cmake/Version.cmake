cmake_minimum_required (VERSION 3.8)

#
# PowerTip Version
#

set (POWERTIP_VERSION_MAJOR 0)
set (POWERTIP_VERSION_MINOR 9)
set (POWERTIP_VERSION_PATCH 1)
set (POWERTIP_VERSION_STAGE "dev")

#
# Version from CI
#

if (DEFINED ENV{POWERTIP_VERSION_MAJOR})
    set (POWERTIP_VERSION_MAJOR $ENV{POWERTIP_VERSION_MAJOR})
endif()

if (DEFINED ENV{POWERTIP_VERSION_MINOR})
    set (POWERTIP_VERSION_MINOR $ENV{POWERTIP_VERSION_MINOR})
endif()

if (DEFINED ENV{POWERTIP_VERSION_PATCH})
    set (POWERTIP_VERSION_PATCH $ENV{POWERTIP_VERSION_PATCH})
endif()

if (DEFINED ENV{POWERTIP_VERSION_STAGE})
    set (POWERTIP_VERSION_STAGE $ENV{POWERTIP_VERSION_STAGE})
endif()

if (NOT DEFINED POWERTIP_REVISION)
    if (DEFINED ENV{GIT_COMMIT})
        string (SUBSTRING $ENV{GIT_COMMIT} 0 8 POWERTIP_REVISION)
    elseif (POWERTIP_VERSION_STAGE STREQUAL "snapshot")
        execute_process (
            COMMAND git rev-parse --short=8 HEAD
            WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
            OUTPUT_VARIABLE POWERTIP_REVISION
            OUTPUT_STRIP_TRAILING_WHITESPACE
        )
    endif()
endif()

if (DEFINED POWERTIP_REVISION)
    string(LENGTH ${POWERTIP_REVISION} POWERTIP_REVISION_LENGTH)
    if (NOT ((POWERTIP_REVISION MATCHES "^[a-f0-9]+") AND (POWERTIP_REVISION_LENGTH EQUAL "8")))
        message (FATAL_ERROR "POWERTIP_REVISION ('${POWERTIP_REVISION}') should be a short commit hash")
    endif()
    unset (POWERTIP_REVISION_LENGTH)
else()
	set (POWERTIP_REVISION "0badc0de")
endif()

if (DEFINED ENV{BUILD_NUMBER})
    set (POWERTIP_BUILD_NUMBER $ENV{BUILD_NUMBER})
else()
    set (POWERTIP_BUILD_NUMBER 1)
endif()

string (TIMESTAMP POWERTIP_BUILD_DATE "%Y%m%d" UTC)
set (POWERTIP_SNAPSHOT_INFO ".${POWERTIP_VERSION_STAGE}.${POWERTIP_REVISION}")

if (POWERTIP_VERSION_STAGE STREQUAL "snapshot")
    set (POWERTIP_VERSION_TAG "${POWERTIP_VERSION_STAGE}.b${POWERTIP_BUILD_NUMBER}-${POWERTIP_REVISION}")
else()
    set (POWERTIP_VERSION_TAG "${POWERTIP_VERSION_STAGE}")
endif()

set (POWERTIP_VERSION "${POWERTIP_VERSION_MAJOR}.${POWERTIP_VERSION_MINOR}.${POWERTIP_VERSION_PATCH}")
set (POWERTIP_VERSION_STRING "${POWERTIP_VERSION}-${POWERTIP_VERSION_TAG}")
message (STATUS "Full PowerTip version string is '" ${POWERTIP_VERSION_STRING} "'")

add_definitions (-DPOWERTIP_VERSION="${POWERTIP_VERSION}")
add_definitions (-DPOWERTIP_VERSION_STRING="${POWERTIP_VERSION_STRING}")
add_definitions (-DPOWERTIP_REVISION="${POWERTIP_REVISION}")
add_definitions (-DPOWERTIP_BUILD_DATE="${POWERTIP_BUILD_DATE}")
add_definitions (-DPOWERTIP_BUILD_NUMBER=${POWERTIP_BUILD_NUMBER})

if (POWERTIP_DEVELOPER_MODE)
    add_definitions (-DPOWERTIP_DEVELOPER_MODE=1)
endif()

if (POWERTIP_ENTERPRISE)
    add_definitions (-DPOWERTIP_ENTERPRISE=1)
endif()
